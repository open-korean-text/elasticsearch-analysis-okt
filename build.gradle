group 'org.openkoreantext'
version '6.0.0.0'

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'jacoco'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
    jcenter()
}

configurations {
    distJars {
        extendsFrom runtime
    }
}

ext {
    elasticsearchVersion = '6.0.0-rc1'
    openKoreanTextVersion = '2.1.0'
}

dependencies {
    compile group: 'org.openkoreantext', name: 'open-korean-text', version: openKoreanTextVersion

    compileOnly group: 'org.elasticsearch', name: 'elasticsearch', version: elasticsearchVersion

    testCompile group: 'org.elasticsearch.test', name: 'framework', version: elasticsearchVersion
    testCompile group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.9.1'
    testCompile group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.9.1'
}

task makePluginDescriptor(type: Copy) {
    from 'src/main/resources'
    into 'build/tmp/plugin'
    expand([
        'descriptor': [
            'name': 'elasticsearch-analysis-openkoreantext',
            'classname': 'org.elasticsearch.plugin.analysis.openkoreantext.AnalysisOpenKoreanTextPlugin',
            'description': 'Korean analysis plugin integrates open-korean-text module into elasticsearch.',
            'version': '1.0.0',
            'javaVersion': sourceCompatibility,
            'elasticsearchVersion' : elasticsearchVersion
        ]
    ])
}

task buildPluginZip(type: Zip, dependsOn: [':jar', ':makePluginDescriptor']) {
    from configurations.distJars
    from jar.archivePath
    from 'build/tmp/plugin'
    into 'elasticsearch'
    classifier = 'plugin'
}

build.finalizedBy(buildPluginZip)

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

signing {
    sign configurations.archives
}

signArchives.enabled = false

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'elasticsearch-analysis-openkoreantext'
                packaging 'jar'
                description 'Korean analysis plugin integrates open-korean-text module into elasticsearch.'
                url 'https://github.com/open-korean-text/elasticsearch-analysis-openkoreantext'

                scm {
                    connection 'scm:git:git://github.com/open-korean-text/elasticsearch-analysis-openkoreantext.git'
                    developerConnection 'scm:git:ssh://github.com:open-korean-text/elasticsearch-analysis-openkoreantext.git'
                    url 'https://github.com/open-korean-text/elasticsearch-analysis-openkoreantext/tree/master'
                }

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'keepcosmos'
                        name 'Jaehyun Shin'
                        email 'keepcosmos@gmail.com'
                    }
                }
            }
        }
    }
}


test {
    jvmArgs '-Dtests.security.manager=false'
}

jacoco {
    toolVersion = '0.7.1.201405082137'
}

jacocoTestReport {
    reports {
        html.enabled = true
        xml.enabled = true
        csv.enabled = false
    }
}

task jacocoRootReport(type: org.gradle.testing.jacoco.tasks.JacocoReport) {
    sourceDirectories = files(sourceSets.main.allSource.srcDirs)
    classDirectories =  files(sourceSets.main.output)
    executionData = files(jacocoTestReport.executionData)
    reports {
        html.enabled = true
        xml.enabled = true
        csv.enabled = false
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.4'
}
